/*
 * Copyright 2020 Szymon Micyk
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'groovy'
    id 'application'
    id 'idea'
    id 'eclipse'
    id 'ivy-publish'
    id 'codenarc'
    id 'pl.allegro.tech.build.axion-release' version '1.12.1'
}

repositories {
    mavenCentral()
}

scmVersion {
    tag {
        prefix = 'v'
        versionSeparator = ''
        initialVersion = { config, position -> '0.1.0' }
    }

    hooks {
        pre 'commit', { version, position -> "Release $version"}
    }
}

project.version = scmVersion.version

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    implementation('org.codehaus.groovy:groovy-all:2.5.7')
    implementation('commons-fileupload:commons-fileupload:1.4')

    runtime('javax.servlet:servlet-api:2.4')

    // testing and mocking
    testCompile('junit:junit:4.12')
    testCompile('org.spockframework:spock-core:1.3-groovy-2.5')
}

codenarc {
    toolVersion = '1.4'
    configFile = file("${rootProject.projectDir}/config/codenarc/rules.groovy")
    reportFormat = 'html'
    ignoreFailures = true
}

//* Test configuration *//
tasks.withType(Test) {
    testLogging {
        events 'started', 'passed'
    }
}

application {
    mainClassName = 'net.simonix.scripts.harventer.Script'
    applicationName = 'harventer'
}

publishing {
    repositories {
        ivy {
            artifactPattern "${System.properties['user.home']}/.groovy/grapes/[organization]/[module]/jars/[artifact]-[revision](.[ext])"
            ivyPattern "${System.properties['user.home']}/.groovy/grapes/[organization]/[module]/ivy-[revision].xml"
        }
    }
    publications {
       ivy(IvyPublication) {
            organisation = 'net.simonix.scripts'
            module = 'harventer'
            revision = project.version

            descriptor.status = 'release'

            from components.java
        }
    }
}
